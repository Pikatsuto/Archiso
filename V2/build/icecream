# Maintainer: Ben Widawsky <ben@bwidawsk.net>
# Contributor: Isaac C. Aronson <isaac@pingas.org> (original PKGBUILD)
# Contributor: Sergio Correia <sergio@correia.cc> (modifications derived from icecream-git package)

pkgname=icecream
pkgver=1.4
pkgrel=4
pkgdesc="takes compile jobs from your build and distributes it to remote machines allowing a parallel build on several machines."
url="https://github.com/icecc/icecream"
license=('GPL2')
makedepends=('docbook2x' 'clang')
depends=('bash' 'libcap-ng' 'libarchive' 'lzo' 'zstd')
provides=('icecream')
optdepends=('icecream-sundae: A commandline monitor for Icecream'
            'icemon: Icecream GUI monitor'
            'ccache: ccache enabled remote builds.'
            'clang: clang enabled remote builds.')
conflicts=('icecream-git')
backup=('etc/icecream.conf')
arch=('x86_64')
install=icecream.install
source=("https://github.com/icecc/$pkgname/archive/refs/tags/$pkgver.tar.gz"
        icecream.conf
        icecream.service
        icecream-scheduler.service
        icecreamd
        icecream-schedulerd
        ld-icecream.conf)
sha256sums=( '' )





build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./autogen.sh
  if command -v clang >/dev/null;
  then
    ./configure --prefix=/usr/lib/icecream --enable-shared --disable-static --enable-clang-wrappers --enable-clang-rewrite-includes --mandir=/usr/share/man
  else
    ./configure --prefix=/usr/lib/icecream --enable-shared --disable-static --mandir=/usr/share/man
  fi
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -D -m755 "$srcdir"/icecream.conf "$pkgdir/etc/icecream.conf"
  install -D -m755 "$srcdir"/icecreamd "$pkgdir/usr/lib/icecream/icecreamd"
  install -D -m755 "$srcdir"/icecream-schedulerd "$pkgdir/usr/lib/icecream/icecream-schedulerd"
  install -D -m644 "$srcdir"/icecream-scheduler.service "$pkgdir/usr/lib/systemd/system/icecream-scheduler.service"
  install -D -m644 "$srcdir"/icecream.service "$pkgdir/usr/lib/systemd/system/icecream.service"
  install -D -m644 "$srcdir"/ld-icecream.conf "${pkgdir}/etc/ld.so.conf.d/icecream.conf"

  # moving pkg-config file to its usual place
  install -D -m644 "${pkgdir}/usr/lib/icecream/lib/pkgconfig/icecc.pc" "${pkgdir}/usr/lib/pkgconfig/icecc.pc"
  rm -rf "${pkgdir}/usr/lib/icecream/lib/pkgconfig"
}
# vim:set ts=2 sw=2 et:
